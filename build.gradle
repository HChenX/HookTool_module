plugins {
    id 'com.android.library'
}

android {
    namespace 'com.hchen.hooktool'
    compileSdk 34

    defaultConfig {
        minSdk 29
        //noinspection OldTargetApi
        targetSdk 34
        // noinspection HighAppVersionCode
        versionCode 2025011300
        versionName 'v.1.1.7'

        aarMetadata {
            minCompileSdk = 29
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    buildFeatures {
        buildConfig false
    }

    tasks.register("deleteXposedLogClass") {
        doLast {
            def buildDir = "${project.buildDir}/intermediates/javac/"
            def classFiles = fileTree(dir: buildDir, includes: ['**/XposedLog.class'])

            classFiles.each { file ->
                if (file.exists()) {
                    file.delete()
                    println("Deleted ${file}")
                }
            }
        }
    }

    tasks.configureEach { task ->
        def name = task.name
        if (name.contains("JavaWithJavac") && !name.contains("Test")) {
            task.finalizedBy(deleteXposedLogClass)
        }
    }
}

dependencies {
    compileOnly 'de.robv.android.xposed:api:82'
    implementation 'androidx.annotation:annotation:1.9.1'
}
